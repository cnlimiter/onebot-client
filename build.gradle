plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

version = project.sdk_version
group = project.maven_group

def targetJavaVersion = 8

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release = targetJavaVersion
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    archivesBaseName = project.archives_base_name

}

configurations {
    shadow
    implementation.extendsFrom shadow
}


repositories {
    maven {
        url "https://maven.nova-committee.cn/releases"
    }
    mavenCentral()
}


dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.24'
    compileOnly 'com.google.code.gson:gson:2.8.9'
    compileOnly 'org.jetbrains:annotations:20.1.0'
    compileOnly("org.apache.logging.log4j:log4j-api:2.19.0")
    compileOnly("org.apache.logging.log4j:log4j-core:2.19.0")
    testCompileOnly 'org.projectlombok:lombok:1.18.24'
    testRuntimeOnly 'com.google.code.gson:gson:2.8.9'
    testRuntimeOnly 'org.slf4j:slf4j-simple:2.0.1'
    shadow 'cn.evole.bot:OneBot-SDK:0.2.5'
    shadow 'org.java-websocket:Java-WebSocket:1.5.3'

    annotationProcessor 'org.java-websocket:Java-WebSocket:1.5.3'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'

}

artifacts {
    archives jar
    archives shadowJar
}


shadowJar {
    project.configurations.shadow.setTransitive(false);
    configurations = [project.configurations.shadow]
    relocate 'org.java_websocket', "cn.evole.onebot.sdk.websocket"
    dependencies {
        exclude(dependency('org.slf4j:slf4j-api:2.0.1'))
    }
    archiveClassifier = ""
    archiveBaseName.set(project.archives_base_name)
    archiveVersion.set(project.sdk_version)

}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
            version = "${project.sdk_version}"
            artifactId = "${project.archives_base_name}"
            groupId = "${project.maven_group}"
        }
    }

    repositories {
        if (System.getenv('MAVEN_USERNAME') != null && System.getenv('MAVEN_PASSWORD') != null) {
            maven {
                name 'release'
                url = 'https://maven.nova-committee.cn/releases'

                credentials {
                    username System.getenv('MAVEN_USERNAME')
                    password System.getenv('MAVEN_PASSWORD')
                }
            }
        }
    }
}
